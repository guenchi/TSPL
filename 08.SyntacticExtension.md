# 8. 语法扩展

*语法扩展*，又称 *宏*，可以简化和规范程序里重复的语句。通过在程序中引入宏来对程序体进行变换可以写出更高效的程序。

语法扩展通常以 `(keyword subform ...)` 的形式出现，其中 `keyword` 是一个标识符，表示语法扩展的名字，且不同的语法扩展可以有不同形式的 `subform`，有些语法扩展还可以接受伪列表（improper list）和单独一个标识符。

语法扩展由即关键字和相应的转换器（transformer）组成。语法扩展通过 `define-syntax`、`let-syntax` 或者 `letrec-syntax` 来定义。`syntax-rules` 可以用来创建转换器，其支持简单的模式匹配。转换器也可以是一个普通的过程，其接受一个参数并执行任意的计算。对后者而言，`syntax-case` 通常用来解构输入的表达式，`syntax` 用来构造返回的表达式。过程 `identifier-syntax` 和 `make-variable-transformer` 创建的语法扩展可以匹配单个标识符和赋值语句，前者像 `syntax-rules` 一样只能进行简单的模式匹配，而后者可以执行任意的计算。

在编译或解释程序前，语法扩展会被语法展开器（expander）展开为核心语句（core forms）。当展开器遇到了一个语法扩展，它会调用相应的转换器来展开程序，再继续处理展开后的结果；当展开器遇到了一个核心语句，它会递归地展开它的参数（如果有的话）。展开器会在展开的过程中维护标识符的绑定，以保证变量和关键字的作用域。

本章介绍的语法扩展的机制是 "syntax-case" 系统的一部分。该系统的一个可移植的实现可在 <http://www.cs.indiana.edu/syntax-case/> 获取。这一系统的动机和实现参见 "Syntactic Abstraction in Scheme" \[[12](References.md#g230)\]。一些额外的未进入标准的功能参见 *Chez Scheme User's Guide* \[[9](References.md#g227)\]，它们包括 `modules`、局部 `import` 和元定义（meta definitions）。

## 8.1 关键字绑定

本节介绍将关键字（keyword）与转换器绑定的方法。在顶层程序或库里可以通过 `define-syntax` 进行关键字绑定，在局部作用域里的绑定则可以通过 `define-syntax`、`let-syntax` 或 `letrec-syntax`。

+ 语法：`(define-syntax keyword expr)`
+ 库： `(rnrs base)`, `(rnrs)`

`expr` 的值必须是一个转换器。

下面的例子将 `let*` 定义为语法扩展，其转换器由 `syntax-rules` 创建（见 [8.2](#82-syntax-rules-转换器) 节）。

```
(define-syntax let*
  (syntax-rules ()
    [(_ () b1 b2 ...) (let () b1 b2 ...)]
    [(_ ((i1 e1) (i2 e2) ...) b1 b2 ...)
     (let ([i1 e1])
       (let* ([i2 e2] ...) b1 b2 ...))]))
```

所有局部定义创建的绑定，无论是关键字还是变量，在局部的任何地方（包括定义本身内部）都是可见的。例如，表达式
```
(let ()
  (define even?
    (lambda (x)
      (or (= x 0) (odd? (- x 1)))))
  (define-syntax odd?
    (syntax-rules ()
      [(_ x) (not (even? x))]))
  (even? 10))
```
是合法的，其值为 `#t`。

展开器以从左到右的顺序处理 `library`、`lambda` 或者其他程序体里的程序。当它遇到一个变量定义，就会记录下被定义的关键字是个变量这一事实，但是右侧表达式的求值会被延迟，直到所有的定义都处理完。当它遇到一个关键字定义，就会将右侧表达式展开并求值，并把得到的转换器绑定给被定义的关键字。当它遇到一个表达式，就会把所有延迟的右侧表达式全部展开，包括当前的表达式和当前程序体内余下的表达式。

这一从左到右的求值顺序意味着，一个局部定义可以影响随后的语句是否也是定义。例如，表达式
```
(let ()
  (define-syntax bind-to-zero
    (syntax-rules ()
      [(_ id) (define id 0)]))
  (bind-to-zero x)
  x)
```
的值是 0，不论 `let` 语句外部的 `bind-to-zero` 是否被绑定为任何其他值。

+ 语法：`(let-syntax ((keyword expr) ...) form1 form2 ...)`
+ 语法：`(letrec-syntax ((keyword expr) ...) form1 form2 ...)`
+ 返回：见下文
+ 库：`(rnrs base)`, `(rnrs)`

每一个 `expr` 的值必须是一个转换器。二者都在语句 `form1 form2 ...` 中将 `keyword` 绑定为相应的转换器。`letrec-syntax` 的 `expr` 中 `keyword` 的绑定是可见的。

在任何表达式被允许的地方，`let-syntax` 或 `letrec-syntax` 语句展开为一个或多个表达式，这些表达式被视作如同被 `begin` 括起来一样。因此在任何允许定义的地方，当语句是定义的时候，它们也会展开成定义，相当于这些定义出现在了 `let-syntax` 或 `letrec-syntax` 的位置。

下面的例子展示了 `let-syntax` 和 `letrec-syntax` 的不同之处。
```
(let ([f (lambda (x) (+ x 1))])
  (let-syntax ([f (syntax-rules ()
                       [(_ x) x])]
               [g (syntax-rules ()
                       [(_ x) (f x)])])
    (list (f 1) (g 1)))) => (1 2)

(let ([f (lambda (x) (+ x 1))])
  (letrec-syntax ([f (syntax-rules ()
                       [(_ x) x])]
                  [g (syntax-rules ()
                       [(_ x) (f x)])])
    (list (f 1) (g 1)))) => (1 1)
```
这两个表达式除了前一个用 `let-syntax` 而后一个用 `letrec-syntax` 以外，其余都是相同的。在第一个表达式里，`g` 中的 `f` 指代的是 `let` 表达式绑定的变量 `f`，而第二个表达式里 `g` 中的 `f` 指代的是 `letrec-syntax` 绑定的关键字 `f`。

## 8.2 Syntax-Rules 转换器

本节介绍的 `syntax-rules` 语句允许以一种方便的形式定义简单的转换器。这些转换器可以用 8.1 节的方式绑定给关键字。尽管它的表现力比 [8.3](#83-syntax-case-转换器) 节介绍的机制更弱，但对许多常用的语法扩展已经足够。

+ 语法：`(syntax-rules (literal ...) clause ...)`
+ 返回：一个转换器
+ 库：`(rnrs base)`, `(rnrs)`

每个 `literal` 必须是一个标识符，且不能为下划线（`_`）或省略号（`...`）。每个 `clause` 必须具有下面的形式
```
(pattern template)
```
每个 `pattern` 指定了一个可能的输入模式，相应的 `template` 指定了输出的形式。

模式由列表、向量、标识符和常量组成。模式中出现的标识符必须是 *字面量*、*模式变量*、*下划线* 或者 *省略号*。除了 `_` 或 `...` 以外，一个标识符是字面量，如果它在 `(literal ...)` 中出现；否则，它就是一个模式变量。字面量的作用是充当辅助关键字，例如 `case` 和 `cond` 语句中的 `else`。模式中的列表和向量指定了输入所需的基本结构，而下划线和模式变量则指定了输入可以具有的任意子结构，字面量和常量指定了输入必须严格匹配的原子部分。省略号指定了模式中出现的重复子模式。

一个输入语法形式 `F` 与模式 `P` 匹配，当且仅当：
+ `P` 是一个下划线或者模式变量
+ `P` 是一个字面量，且 `F` 是具有与 `P` 相同绑定的模式变量，是否具有相同绑定由谓词 `free-identifier=?` 决定（见 [8.3](#83-syntax-case-转换器) 节）
+ `P` 是形式 `(P1 ... Pn . Px)` 且 `F` 是一个包含至少 *n* 个元素的列表或者伪列表，其前 *n* 个元素与 *P<sub>1</sub>* 到 *P<sub>n</sub>* 分别匹配，且 `F` 的 *n*-th cdr 与 *P<sub>x</sub>* 匹配
+ `P` 是形式 `(P1 ... Pk Pe ellipsis Pm+1 ... Pn)`（`ellipsis` 表示标识符 `...`），`F` 是 *n* 个元素的真列表，其前 *k* 个元素与 *P<sub>1</sub>* 到 *P<sub>k</sub>* 匹配，接下来的 *m-k* 个元素与 *P<sub>e</sub>* 匹配，剩下的 *n-m* 个元素与 *P<sub>m+1</sub>* 到 *P<sub>n</sub>* 匹配
+ `P` 是形式 `(P1 ... Pk Pe ellipsis Pm+1 ... Pn . Px)`（`ellipsis` 表示标识符 `...`），`F` 是 *n* 个元素的列表或伪列表，其前 *k* 个元素与 *P<sub>1</sub>* 到 *P<sub>k</sub>* 匹配，接下来的 *m-k* 个元素与 *P<sub>e</sub>* 匹配，剩下的 *n-m* 个元素与 *P<sub>m+1</sub>* 到 *P<sub>n</sub>* 匹配，且 `F` 的 *n*-th cdr 与 *P<sub>x</sub>* 匹配
+ `P` 是形式 `#(P1 ... Pn)` 且 `F` 是一个含 *n* 个元素的向量，其元素与 *P<sub>1</sub>* 至 *P<sub>n</sub>* 匹配
+ `P` 是形式 `#(P1 ... Pk Pe ellipsis Pm+1 ... Pn)`（`ellipsis` 表示标识符 `...`），`F` 是 *n* 个元素的向量，其前 *k* 个元素与 *P<sub>1</sub>* 到 *P<sub>k</sub>* 匹配，接下来的 *m-k* 个元素与 *P<sub>e</sub>* 匹配，剩下的 *n-m* 个元素与 *P<sub>m+1</sub>* 到 *P<sub>n</sub>* 匹配
+ `P` 是一个模式数据（任何不是列表，不是向量，也不是符号的对象），`F` 在谓词 `equal?` 的意义下与 `P` 相等

一个 syntax-rules 模式的最外层结构必须是上面所述列表结构中的一种，尽管任何子结构可以是上述情况的任何一种（而不必是列表）。更进一步，最外层模式的第一个元素将被忽略，因为总是假定它就是命名该语法扩展的关键字。（这段陈述对 `syntax-case` 不适用，见 [8.3](#83-syntax-case-转换器) 节。）

如果传递给 `syntax-rules` 转换器的语句和一个给定分句（clause）的模式相匹配，则该分句被接受，该语句被转换成相应模板（template）指定的语句。当这一变换发生时，模板中的模式变量被绑定为相应的输入子句。当模式变量出现在一个子模板中，而该模板后有一个或多个省略号，则该模式变量会绑定为一个或多个由零个或多个子句组成的序列。

一个模板是一个模式变量，或一个不是模式变量的标识符，或一个模式数据，或一个子模板 `(S1 ... Sn)`  的列表，或一个子模板的伪列表 `(S1 S2 ... Sn . T)`，或一个子模板的向量 `#(S1 ... Sn)`。其中，每个子模板 *S<sub>i</sub>* 都是一个模板，后接零个或多个省略号。伪列表里最后的 `T` 是一个模板。

输出中出现在模板里的模式变量会被输入中它们所绑定的语句取代，模板数据和不是模板变量的标识符被直接插入到输出中。模板中的列表和向量结构在输出中依然是列表和向量结构。一个后接省略号的子模板被展开为零个或多个子模板，这些子模板必须包括至少一个其所在模式后接省略号的模式变量（否则，展开器无法确定模板在输出中应该重复多少次）。子模式中出现的后接一个或多个省略号的模式变量只能出现在后接（至少）相同数量省略号的子模式中，这些模式变量在输出中会被替换为它们所绑定的输入语句。如果一个模式变量在模板中出现的省略号个数比在相应模式中出现的省略号个数多，则输入语句根据需要被重复。

一个形式为 `(... template)` 的模板和 `template` 是等价的，除了 `template` 中的省略号不再有特殊意义。也就是说，`template` 中出现的任何省略号将以普通的标识符对待。特别地，模板 `(... ...)` 和一个省略号 `...` 是等价的，这允许语法扩展的输出里包含省略号。

下面对 `or` 的定义演示了 `syntax-rules` 的用法。
```
(define-syntax or
  (syntax-rules ()
    [(_) #f]
    [(_ e) e]
    [(_ e1 e2 e3 ...)
     (let ([t e1]) (if t t (or e2 e3 ...)))]))
```
输入模式指定了输入必须包括该关键字（即 `or`）和零个或多个子表达式。下划线（`_`）可以匹配输入中的任何表达式，因此关键字的位置通常放下划线，以提示程序员和其他阅读该程序的人，这个匹配关键字的位置永远不可能包含除了该关键字以外的其他内容，从而不需要被匹配。（事实上，正如上面所说，`syntax-rules` 会忽略任何出现在关键字位置的东西。）如果出现多于一个子表达式（第三个分句），则展开的代码会测试第一个子表达式的值，如果非假则返回它。为了避免对一个表达式求值两次，转换器引入了一个临时变量 `t`。

展开器的算法会根据需要对本地标识符重命名以维护其作用域。因此，由转换器引入的变量 `t` 的绑定仅在转换器引入的代码中可见，而在输入的语句中不可见。类似地，对 `let` 和 `if` 的引用不会被输入环境中的绑定影响。

```
(let ([if #f])
  (let ([t 'okay])
    (or if t))) => okay
```
这个表达式在展开后变为以下等价表达式
```
((lambda (if1)
   ((lambda (t1)
      ((lambda (t2)
         (if t2 t2 t1))
       if1))
    'okay))
 #f) => okay
 ```
 在这个展开示例中，原表达式中的 `if` 和 `t`，以及 `or` 展开式中的 `t` 被重命名为 `if1`、`t1` 和 `t2` 。

下面的例子给出了一个简化的 `cond` 的定义（称其简化是因为它的每个分句需要至少一个输出表达式，并且不支持辅助关键字 `=>`），演示了像 `else` 这种辅助关键字是如何通过字面量列表被转换器识别的。
```
(define-syntax cond
  (syntax-rules (else)
    [(_ (else e1 e2 ...)) (begin e1 e2 ...)]
    [(_ (e0 e1 e2 ...)) (if e0 (begin e1 e2 ...))]
    [(_ (e0 e1 e2 ...) c1 c2 ...)
     (if e0 (begin e1 e2 ...) (cond c1 c2 ...))]))
```

+ 语法：`_`
+ 语法： `...`
+ 库：`(rnrs base)`, `(rnrs syntax-case)`, `(rnrs)`

这些标识符是 `syntax-rules`、`identifier-syntax` 和 `syntax-case` 的辅助关键字。后者（`...`）还是 `syntax` 和 `quasisyntax` 的辅助关键字。对这些关键字的引用会导致语法错误，除非它们被用作辅助关键字。

+ 语法：`(identifier-syntax tmpl)`
+ 语法：`(identifier-syntax (id1 tmpl1) ((set! id2 e2) tmpl2))`
+ 返回：转换器
+ 库：`(rnrs base)`, `(rnrs)`

当一个关键字与 `identifier-syntax` 的第一种形式得到的转换器绑定时，在定义域内对该关键字的引用将被替换为 `tmpl`。
```
(let ()
  (define-syntax a (identifier-syntax car))
  (list (a '(1 2 3)) a)) => (1 #<procedure>)
```
使用第一种形式的 `identifier-syntax` 时，任何对关键字的显式赋值（使用 `set!`）会导致语法错误。第二种形式则更加通用，允许指定 `set!` 出现时的行为。
```
(let ([ls (list 0)])
  (define-syntax a
    (identifier-syntax
      [id (car ls)]
      [(set! id e) (set-car! ls e)]))
  (let ([before a])
    (set! a 1)
    (list before a ls))) => (0 1 (1))
```

用 `make-variable-transformer` 定义的 `identifier-syntax` 可以在 [这里](#defn:identifier-syntax) 找到。

## 8.3 Syntax-Case 转换器
